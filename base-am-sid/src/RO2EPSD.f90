! -------------------------------------------------------------------------
!
SUBROUTINE RO2EPSD(ROIKG,TI,FREQ,EPSI,EPSII,NUM)
!
! -------------------------------------------------------------------------
!
!     CODE ORIGINALLY OBTAINED IN MATLAB FROM MATZLER
!
!   CALCULATES THE DIELECTRIC PERMITTIVITY FROM
!   DENSITY FOR DRY SNOW.
!
!   [EPSI,EPSII] = RO2EPSD(ROIKG,TI,FREQ)
!       EPSI:  REAL PART OF DIELECTRIC PERMITTIVITY
!       EPSII: IMAGINARY PART OF DIELECTRIC PERMITTIVITY
!       ROIKG:   DENSITY [KG/M**3]
!       TI:    SNOW TEMPERATURE [KELVIN]
!       FREQ:  FREQUENCY [GHZ]
!
!   VERSION HISTORY:
!      1.0    WI 15.7.95
!      2.0    WI 12.11.97  ENHANCED WITH POLDER AND VAN SANTEN EQUATIONS (SEE
!                           POLDER.M)
!      3.0    MD  1 APR 05 TRANSLATED TO FORTRAN FROM MATLAB
!      3.1    MD 21 NOV 05 CHANGED ALL LOCALS TO DYNAMICALLY ALLOCATABLE
!
!   USES:
!       EPSICE, EPSR, POLDER
!
!   COPYRIGHT (C) 1997 BY THE INSTITUTE OF APPLIED PHYSICS,
!   UNIVERSITY OF BERN, SWITZERLAND

IMPLICIT NONE

INTEGER,INTENT(IN):: NUM
REAL(8), INTENT(IN) :: ROIKG(NUM),TI(NUM),FREQ
REAL(8), INTENT(OUT) :: EPSI(NUM), EPSII(NUM)
INTEGER :: I
REAL(8) :: ER_ICE
REAL(8),DIMENSION(:),ALLOCATABLE :: EI_ICE,V,A,A3,EA,EA3,K1,K3,KSQ


ALLOCATE(EI_ICE(NUM),V(NUM),A(NUM),A3(NUM),EA(NUM),EA3(NUM), &
        K1(NUM),K3(NUM),KSQ(NUM))

!Calculate real part of dry snow, EPSI
CALL EPSRSNOW(ROIKG,NUM,EPSI)

!Calculate imaginary part of dry snow, EPSII
CALL EPSIICE(TI,FREQ,EI_ICE,NUM)


V=ROIKG/917d0
ER_ICE=3.185d0

DO I=1,NUM
    A(I)=0.3d0
    IF (V(I)<0.55d0) THEN
        A(I)=0.476d0-0.64d0*V(I)
    END IF
    IF (V(I)<0.333d0) THEN
        A(I)=0.1d0+0.5d0*V(I)
    END IF
END DO


!Revised according to MEMLS3 code, here the empirical permititivity is used directly, no polder!
!CALL POLDER(V,A,ER_ICE,EPSI,EPSP,NUM)

A3=1d0-2d0*A
EA=(EPSI*(1d0-A))+A
EA3=(EPSI*(1d0-A3))+A3
K1=(EA/(EA+A*(ER_ICE-1d0)))**2d0
K3=(EA3/(EA3+A3*(ER_ICE-1d0)))**2d0
KSQ=(2d0*K1+K3)/3d0

EPSII=V*EI_ICE*KSQ*(EPSI**0.5d0)

DEALLOCATE(EI_ICE,V,A,A3,EA,EA3,K1,K3,KSQ)

END SUBROUTINE RO2EPSD
