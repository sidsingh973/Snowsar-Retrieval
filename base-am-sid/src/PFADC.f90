! -------------------------------------------------------------------------
!
SUBROUTINE PFADC(TETA,DI,EPSI,GS6,DEI,TEI,TSCAT,NUM)
!
! -------------------------------------------------------------------------
!
!      CODE ORIGINALLY OBTAINED IN MATLAB FROM MATZLER
!
!   CALCULATES THE EFFECTIVE PATH LENGTH IN A LAYER
!
!   [DEI,TEI,TSCAT] = PFADC(TETA,DI,EPSI,GS6)
!       DEI:  EFFECTIVE PATH LENGTH [M]
!       TEI:  LOCAL INCIDENCE ANGLE
!       TSCAT: SCATTERING
!       TETA: INCIDENCE ANGLE AT SNOW AIR INTERFACE
!       DI:   THICKNESS [M]
!       EPSI: DIELECTRIC PERMITTIVITY
!       GS6:  6-FLUX SCATTERING COEFFICIENT
!
!   VERSION HISTORY:
!      1.0    WI 15.10.97
!      2.0    MD 1 APR 05 TRANSLATED TO FORTRAN FROM MATLAB
!      2.1    MD 21 NOV 05 CHANGED ALL LOCALS TO ALLOCATBLE
!
!
!   USES: - NONE
!
!   COPYRIGHT (C) 1997 BY THE INSTITUTE OF APPLIED PHYSICS,
!   UNIVERSITY OF BERN, SWITZERLAND

IMPLICIT NONE

INTEGER, INTENT(IN) :: NUM
REAL(8), INTENT(IN) :: TETA,DI(NUM),EPSI(NUM),GS6(NUM)
REAL(8), INTENT(OUT) :: DEI(NUM),TEI(NUM),TSCAT(NUM)
REAL(8),DIMENSION(:),ALLOCATABLE ::  NS,COSTETASN,COSC,COSTETASC,TAUSCAT,COSTETA
INTEGER I

ALLOCATE( NS(NUM),COSTETASN(NUM),COSC(NUM),COSTETASC(NUM),TAUSCAT(NUM+1),&
COSTETA(NUM) )

NS=EPSI**0.5d0
COSTETASN=(1d0-(SIN(TETA)/NS)**2)**0.5d0
COSC=(1d0-(1d0/NS)**2d0)**0.5d0
COSTETASC=0.5d0*(1d0+COSC)
DEI=DI/COSTETASN

TAUSCAT(NUM+1)=0d0

DO I=NUM,1,-1
    TAUSCAT(I)=TAUSCAT(I+1)+DEI(I)*GS6(I)/2
    TSCAT(I)=EXP(-1*TAUSCAT(I))
    COSTETA(I)=TSCAT(I)*COSTETASN(I)+(1-TSCAT(I))*COSTETASC(I)
END DO

TEI=ACOS(COSTETA)

DEALLOCATE (  NS,COSTETASN,COSC,COSTETASC,TAUSCAT,COSTETA )

END SUBROUTINE PFADC
