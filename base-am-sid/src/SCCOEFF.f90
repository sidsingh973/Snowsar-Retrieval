! -------------------------------------------------------------------------
!
SUBROUTINE SCCOEFF(ROI,TI,PCI,FREQ,WIFR,GAI,SCCHO,GBIH,GBIV,GS6,&
        GA2I,NUM,EPSI,EPSII)

!
! -------------------------------------------------------------------------
!
! CODE ORIGINALLY OBTAINED IN MATLAB FROM MATZLER, MOD. BY MIKE (SEE HISTORY)
!
!   CALCULATES THE SCATTERING COEFFICIENT FROM STRUCTURAL PARAMETERS
!     DIFFERENT ALGORITHMS CAN BE CHOSEN, BY CHANGING "SCCHO"
!
!   [GBIH,GBIV,GS6,GA2I] = SCCOEFF(ROI,TI,PCI,FREQ,WI,GAI,SCCHO)
!       GBIH:  2-FLUX SCATTERING COEFFICIENT AT H POL
!       GBIV:  2-FLUX SCATTERING COEFFICIENT AT V POL
!       GS6:   6-FLUX SCATTERING COEFFICIENT
!       GA2I:  2-FLUX ABSORPTION COEFFICIENT
!       ROI:   DENSITY [kg/m^3]
!       TI:    PHYSICAL TEMPERATURE [K]
!       PCI:   CORRELATION LENGTH [mm]
!       FREQ:  FREQUENCY [GHz]
!       WI:    WETNESS  [FRAC]
!       GAI:   ABSORPTION COEFFICIENT
!       SCCHO: SCATTERING COEFFICIENT ALGORITHM CHOSEN (1=Empirical MEMLS, 2=MEMLS-IBA)
!
!   VERSION HISTORY:
!      1.0B    WI 15.7.95
!      1.0     WI 23.9.97 BUG FIXED
!      1.1     WI 26.9.97 LATEST FIT ON EXPERIMENTAL DATA WAS ADDED (OPTION 7)
!      1.2     WI 13.10.97 OPTION 8 ADDED, ADAPTED SCATTERING OF A SHELL/SPHERE
!                            TO NOTE 9/VER2
!      1.3     WI  4.11.97 OPTION 9, 10 AND 11 ADDED
!      1.4     WI 27.05.98 BORN APPROXIMATION ADDED (BORNA.M)
!      2.0     MD 1 APR 05 CALL TO BORNA CHANGED TO CALL TO BORN (BY MIKE).
!                            ALL OPTIONS FOR OTHER SHAPES REMOVED BUT BORN
!                            APPROX AND THE APPROX FOR FINE GRAIN SNOW IN
!                            WIESMANN AND MATZLER 1999
!      2.0     MD 21 NOV 05  MADE ALL LOCALS DYNAMICALLY ALLOCATABLE
!
!   USES: BORN
!
!
!   COPYRIGHT (C) 1997 BY THE INSTITUTE OF APPLIED PHYSICS,
!   UNIVERSITY OF BERN, SWITZERLAND

IMPLICIT NONE

INTEGER, INTENT(IN) :: NUM,SCCHO
REAL(8), INTENT(IN) :: ROI(NUM),TI(NUM),PCI(NUM),FREQ,WIFR(NUM),&
GAI(NUM),EPSI(NUM),EPSII(NUM)
REAL(8), INTENT(OUT) :: GBIH(NUM),GBIV(NUM),GS6(NUM),GA2I(NUM)
!REAL(8) C,ROAIR,ROICE,DGB0H,DGB0V,K,EICE,PI
REAL(8) C,ROAIR,ROICE,DGB0H,DGB0V,K,PI
REAL(8) :: EICE(NUM)
REAL(8),DIMENSION(:),ALLOCATABLE ::  VFI,KP,GB6,GC6,GF6,GTR,OMEGA
INTEGER :: I,IERR !IERR USED FOR MPI_FINALIZE IF WE HAVE AN ERROR



ALLOCATE( VFI(NUM),KP(NUM),GB6(NUM),GC6(NUM),GF6(NUM),GTR(NUM),OMEGA(NUM) )

! CONSTANTS
PI=3.14159265358979d0
!C=2.99d0
C=2.99793d0
ROAIR=0.001293d0
ROICE=917d0

! SPECULAR COMPONENT OF SCATTERING COEFFICIENT
!   USUALLY 0 CAN BE IMPORTANT IN NEW SNOW!
DGB0H=0.0d0
DGB0V=0.0d0
K=FREQ*2.0d0*PI/0.299793d0
!EICE=3.18d0  !To be Revised
!Jinmei, Jul 12, Add Real Part of Ice, according to MEMLS3&a
EICE = 3.1884d0 + 9.1d-4*(TI-273.0)

VFI=ROI/ROICE

IF (SCCHO==1) THEN
    GS6=(9.2d0*PCI-1.23d0*ROI/1000.0d0+0.54d0)**2.5d0*(FREQ/50.0d0)**2.5d0
    OMEGA=((EPSI-1)/EPSI)**0.5d0
    GB6=0.5d0*GS6*(1-OMEGA)
    GC6=0.25d0*GS6*OMEGA
ELSEIF (SCCHO==2) THEN
    ! BORN APPROXIMATION
    ! Revised by Jinmei May 9, 2016. Add Born Approximation
    CALL BORN2(K,VFI,PCI,EPSI,EICE,GB6,GC6,GF6,GS6,NUM)
END IF


! COMPUTE OTHER SCATTERING COEFFICIENTS
GTR=(4*GC6)/(GAI+2*GC6)
GA2I=GAI*(1+GTR)
GBIH=(GB6+DGB0H)+GTR*GC6
GBIV=(GB6+DGB0V)+GTR*GC6

DEALLOCATE( VFI,KP,GB6,GC6,GF6,GTR,OMEGA )

END SUBROUTINE SCCOEFF
