SUBROUTINE RANDNORMAL(NUM,Z,SEEDNUM)
!
! ----------------------------------------------------------------------
!
!  GENERATES 'NUM' RANDOM NORMAL NUMBERS WITH ZERO MEAN AND UNIT VARIANCE
!
!  ALGORITHM FROM LEVA, 1992, IN ACM TRANSACTIONS ON MATHEMATICAL SOFTWARE 18(4)
!  FOR SUCCINT DESCRIPTION, SEE THE LIST OF STEPS ON PP. 451-452.
!
!  THIS CODE IS MODIFIED TO ALLOW FOR FUNCTIONALITY WITH MULTIPLE PROCESSORS.
!  THE MODIFICATION IS ESSENTIALLY ONE EXTRA INPUT WHICH IS USED TO SEED
!  THE RANDOM GENERATOR IN SUCH A WAY THAT IT WILL RETURN DIFFERENT RANDOM
!  VARIATES THAN EACH OTHER PROCESSOR.
!
!  CALLS: NONE
!
!  VERSION HISTORY: WRITTEN - MD - MAY 2005
!                   1.1     - MD - AUG 2005 MODIFIED FOR MULTIPLE PROCESSORS
!
!  NUM - NUMBER OF VARIATES TO GENERATE
!  Z - OUTPUT ARRAY OF VARIATES
!  SEEDNUM - SINGLE NUMBER USED IN SEED FOR THE RANDOM NUMBER GENERATOR

IMPLICIT NONE

INTEGER,INTENT(IN)::NUM,SEEDNUM
REAL(8),INTENT(OUT)::Z(NUM)
INTEGER N,RSZ,sout(2),I
Integer,Dimension(:),Allocatable :: Seed
REAL(8) U,V,S,T,A,B,X,Y,Q

! DEFINE CONSTANTS
S=0.449871d0
T=-0.386595d0
A=0.19600d0
B=0.25472d0

! RSZ IS THE NUBMER OF INTEGERS USED TO HOLD THE SEED.  THIS IS OBTAINED
! WITH THE CALL TO RANDOM_SEED USING THE 'SIZE' ARGUMENT.
CALL RANDOM_SEED(SIZE=RSZ)

! NOW THE SEED IS COMPUTED BY ADDING THE PROCESS # AND THE NUMBER I=1,RSZ
Allocate(Seed(Rsz))
DO I=1,RSZ
  SEED(I)=SEEDNUM+I
END DO

! FINALLY THE SEED IS SET USING THE SEED VARIABLE
CALL RANDOM_SEED(PUT=SEED(1:RSZ))

N=0
DO WHILE(N<NUM)
  ! 1. GENERATE A POINT P=(U,V) UNIFORM IN R
  CALL RANDOM_NUMBER(U)
  CALL RANDOM_NUMBER(V)
  V=1.7156d0*(V-0.5d0) !SCALE V TO BE UNIFORM IN (-R,R) WHERE R=2/e = 0.8578

  ! 2. EVALUATE QUADRATIC FORM OF Q
  X=U-S
  Y=ABS(V)-T
  Q=X**2d0+Y*(A*Y-B*X)

  ! 3. DECIDE WHETHER TO ACCEPT OR REJECT THIS POINT
  IF (Q<0.27597d0) THEN
    !ACCEPT P IF Q IS LESS THAN INNER BOUNDARY VALUE
    N=N+1
    Z(N)=V/U
  ELSEIF (Q>0.27846d0 .OR. V**2d0>-4d0*U**2d0*LOG(U)) THEN
    !REJECT THIS ONE IF IF Q IS GREATER THAN OUTER BOUNDARY VALUE OR IF Q IS
    !  OUTSIDE THE ACCEPTANCE REGION A
  ELSE
    !ACCEPT
    N=N+1
    Z(N)=V/U
  END IF
END DO  

Deallocate(Seed)

END SUBROUTINE RANDNORMAL
