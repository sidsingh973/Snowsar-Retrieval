! -------------------------------------------------------------------------
!
SUBROUTINE INTEGRFI(XX,MUI,MUO,STEPS,INTEGR,NUM)
!
! -------------------------------------------------------------------------
!
!   CODE ORIGINALLY OBTAINED IN MATLAB FROM MATZLER
!
!   CALCULATES INTEGRATION  OVER INCIDENT DIRECTIONS, MUI
!       FROM MINI TO MAXI IN STEPS INTERVALS OF THE FI
!       PHASE FUNCTION.
!
!
!   INTEGR = INTEGRFI(XX,MUI,MUO,STEPS)
!
!   VERSION HISTORY:
!      1.0     WI 27.05.98
!      2.0    MD 1 APR 05 TRANSLATED TO FORTRAN FROM MATLAB
!      2.1    MD 21 NOV 05 CHANGED ALL LOCALS TO ALLOCATABLE
!
!   USES: - NONE
!
!   COPYRIGHT (C) 1998 BY THE INSTITUTE OF APPLIED PHYSICS,
!   UNIVERSITY OF BERN, SWITZERLAND

IMPLICIT NONE

INTEGER,INTENT(IN) :: NUM,STEPS
REAL(8), INTENT(IN) :: XX(NUM),MUI(NUM),MUO(NUM)
REAL(8), INTENT(OUT) :: INTEGR(NUM)
REAL(8) PI,DELTA,FI,F0,COFI,SI2FI

INTEGER :: IFI

REAL(8),DIMENSION(:),ALLOCATABLE ::  X2,SII,SIO,COSTE,SI2CHI,FUNC

ALLOCATE( X2(NUM),SII(NUM),SIO(NUM),COSTE(NUM),SI2CHI(NUM),FUNC(NUM) )

PI=3.14159d0
DELTA=PI/STEPS
F0=0.5d0*DELTA
INTEGR=0d0
X2=2d0*XX**2d0

DO IFI=1,STEPS
    FI=F0+(IFI-1d0)*DELTA
    SII=(1-MUI**2d0)**0.5d0
    SIO=(1-MUO**2d0)**0.5d0
    COFI=COS(FI)
    SI2FI=1-COFI**2d0
    COSTE=MUI*MUO+SII*SIO*COFI
    SI2CHI=0.5d0*(1+COSTE**2d0)
    FUNC=SI2CHI/(1+(1-COSTE)*X2)**2d0
    INTEGR=INTEGR+FUNC*DELTA
END DO

INTEGR=INTEGR/PI

DEALLOCATE( X2,SII,SIO,COSTE,SI2CHI,FUNC )

END SUBROUTINE INTEGRFI


