! -------------------------------------------------------------------------
!
SUBROUTINE RT(GAI,GBI,DEI,RI,TI,NUM)
!
! -------------------------------------------------------------------------
!
!     CODE ORIGINALLY OBTAINED IN MATLAB FROM MATZLER
!
!   CALCULATES THE LAYER REFLECTIVITY AND TRANSMISSIVITY
!
!
!   [RI,TI] = RT(GAI,GBI,DEI)
!       RI:   LAYER REFLECTIVITY
!       TI:   LAYER TRANSMISSIVITY
!       GAI:  ABSORPTION COEFFICIENT
!       GBI:  SCATTERING COEFFICIENT
!       DEI:  PATH LENGTH
!
!   VERSION HISTORY:
!      1.0    WI 15.7.95
!      2.0    MD 1 APR 05 TRANSLATED TO FORTRAN FROM MATLAB
!
!   USES: NONE
!
!   COPYRIGHT (C) 1997 BY THE INSTITUTE OF APPLIED PHYSICS,
!   UNIVERSITY OF BERN, SWITZERLAND

IMPLICIT NONE

INTEGER,INTENT(IN) :: NUM
REAL(8),INTENT(IN) :: GAI(NUM),GBI(NUM),DEI(NUM)
REAL(8),INTENT(OUT) :: RI(NUM),TI(NUM)
REAL(8),DIMENSION(:),ALLOCATABLE :: MYGAMMA,T0I,R0I,T02,R02
INTEGER I

ALLOCATE( MYGAMMA(NUM),T0I(NUM),R0I(NUM),T02(NUM),R02(NUM) )

MYGAMMA=(GAI*(GAI+2*GBI))**0.5d0
T0I=EXP(-1*MYGAMMA*DEI)

DO I=1,NUM
    IF(GBI(I)>0.00001d0)THEN
        R0I(I)=GBI(I)/(GAI(I)+GBI(I)+MYGAMMA(I))
    ELSE
        R0I(I)=0d0
    END IF
END DO

T02=T0I**2d0
R02=R0I**2d0
RI=R0I*(1d0-T02)/(1d0-T02*R02)
TI=T0I*(1d0-R02)/(1d0-T02*R02)

DEALLOCATE( MYGAMMA,T0I,R0I,T02,R02 )

END SUBROUTINE RT
