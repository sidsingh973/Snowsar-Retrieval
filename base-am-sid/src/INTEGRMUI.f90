! -------------------------------------------------------------------------
!
SUBROUTINE INTEGRMUI(XX,MINI,MAXI,MINO,MAXO,STEPS,NUM,INTEGR,ARG_LENGTH)
!
! -------------------------------------------------------------------------
!
!     CODE ORIGINALLY OBTAINED IN MATLAB FROM MATZLER
!
!   CALCULATES INTEGRATION  OVER INCIDENT DIRECTIONS, MUI
!       FROM MINI TO MAXI IN STEPS INTERVALS OF THE FI AND
!       MUO INTEGRATED PHASE FUNCTION.
!
!
!   INTEGR = INTEGRMUI(XX,MINI,MAXI,MINO,MAXO,STEPS)
!
!   VERSION HISTORY:
!      1.0     WI 27.05.98
!      2.0    MD 1 APR 05 TRANSLATED TO FORTRAN FROM MATLAB
!
!   USES: INTEGRMUO
!
!   COPYRIGHT (C) 1998 BY THE INSTITUTE OF APPLIED PHYSICS,
!   UNIVERSITY OF BERN, SWITZERLAND

IMPLICIT NONE

INTEGER,INTENT(IN) :: NUM,STEPS,ARG_LENGTH(2)
REAL(8), INTENT(IN) :: XX(NUM),MINI(NUM),MAXI,MINO(ARG_LENGTH(1)),&
MAXO(ARG_LENGTH(2))
REAL(8), INTENT(OUT) :: INTEGR(NUM)
REAL(8) :: STEPSR
REAL(8),DIMENSION(:),ALLOCATABLE :: DMU,DELTA,F0,MUI,FUNC
INTEGER IMU

ALLOCATE( DMU(NUM),DELTA(NUM),F0(NUM),MUI(NUM),FUNC(NUM) )

STEPSR=REAL(STEPS)
DMU=MAXI-MINI
DELTA=DMU/STEPSR
F0=0.5d0*DELTA
INTEGR=0.0d0

DO IMU=1,STEPS
    MUI=MINI+F0+(IMU-1)*DELTA
    CALL INTEGRMUO(XX,MUI,MINO,MAXO,STEPS,FUNC,NUM,ARG_LENGTH)
    INTEGR=INTEGR+FUNC*DELTA
END DO

INTEGR=INTEGR/DMU

DEALLOCATE( DMU,DELTA,F0,MUI,FUNC )

END SUBROUTINE INTEGRMUI
