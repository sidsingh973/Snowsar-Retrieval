! -------------------------------------------------------------------------
!
SUBROUTINE EPSRSNOW(ROIKG,NUM,EPSI)
!
! -------------------------------------------------------------------------
!
!     CODE ORIGINALLY OBTAINED IN MATLAB FROM MATZLER
!
!   CALCULATES THE DIELECTRIC PERMITTIVITY FOR DRY SNOW FROM
!   DENSITY .
!
!   EPSI = EPSR(ROI)
!       EPSI:  REAL PART OF DIELECTRIC PERMITTIVITY
!       ROIKG:   DENSITY [KG/M^3]
!
!   VERSION HISTORY:
!      1.0    WI 15.7.95
!      1.1    WI 23.9.97 ADDED LOOYENGA FOR SNOW DENSER THAN 0.4 G/CM^3
!      2.0    MD 1 APR 05 TRANSLATED TO FORTRAN FROM MATLAB
!
!   USES:
!       EPSICE
!
!
!
!   COPYRIGHT (C) 1997 BY THE INSTITUTE OF APPLIED PHYSICS,
!   UNIVERSITY OF BERN, SWITZERLAND

IMPLICIT NONE

INTEGER, INTENT(IN) :: NUM
REAL(8), INTENT(IN) :: ROIKG(NUM)
REAL(8), INTENT(OUT) :: EPSI(NUM)
INTEGER :: I
REAL(8) :: EHB, ESB
REAL(8),DIMENSION(:),ALLOCATABLE :: VFI, ROI

ALLOCATE(VFI(NUM),ROI(NUM))

ROI=ROIKG/1000.0d0
VFI=ROI/0.917d0
EHB=0.99913d0
ESB=1.4759d0

DO I=1,NUM
    IF (ROI(I)<=0.4d0) THEN
        EPSI(I)=1d0+1.5995d0*ROI(I)+1.861d0*ROI(I)**3d0
    ELSE
        EPSI(I)=((1d0-VFI(I))*EHB+VFI(I)*ESB)**3d0
    END IF
END DO

DEALLOCATE(VFI,ROI)

END SUBROUTINE EPSRSNOW

