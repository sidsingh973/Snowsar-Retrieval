! -------------------------------------------------------------------------
!
SUBROUTINE FRESNELC(TEI,EPSI,SIH,SIV,NUM)
!
! -------------------------------------------------------------------------
!
!     CODE ORIGINALLY OBTAINED IN MATLAB FROM MATZLER
!
!   FRESNEL REFLECTION COEFFICIENTS (ASSUMING EPS'' = 0)
!     (LAYER N+1 IS THE AIR ABOVE THE SNOWPACK)
!
!   [SIH,SIV] = FRESNEL(TEI,ROI)
!       SIH:  INTERFACE REFLECTIVITY AT H POL
!       SIV:  INTERFACE REFLECTIVITY AT V POL
!       TEI:  LOCAL INCIDENCE ANGLE
!       EPSI: REAL PART OF DIELECTRIC PERMITTIVITY
!
!   VERSION HISTORY:
!      1.0    WI 15.7.97
!      2.0    MD 1 APR 05 TRANSLATED TO FORTRAN FROM MATLAB
!
!   USES:
!       EPSR
!
!
!   COPYRIGHT (C) 1997 BY THE INSTITUTE OF APPLIED PHYSICS,
!   UNIVERSITY OF BERN, SWITZERLAND

IMPLICIT NONE

INTEGER, INTENT(IN) :: NUM
REAL(8), INTENT(IN) :: TEI(NUM+1),EPSI(NUM)
REAL(8), INTENT(OUT) :: SIH(NUM),SIV(NUM)
INTEGER :: I
REAL(8) EPSO,EPSU,TEIN
REAL(8),DIMENSION(:),ALLOCATABLE:: EPSI_LOCAL

ALLOCATE(EPSI_LOCAL(NUM+1))

EPSI_LOCAL(1:NUM)=EPSI
EPSI_LOCAL(NUM+1)=1

DO I=1,NUM
    EPSO=EPSI_LOCAL(I+1) !Real parf of the permittivity above the interface
    EPSU=EPSI_LOCAL(I)   !Real part of permittivity under the interface
    TEIN=TEI(I+1)

    SIH(I)=((EPSO**0.5d0*COS(TEIN)-(EPSU-EPSO*SIN(TEIN)**2d0)**0.5d0)/&
        (EPSO**0.5d0*COS(TEIN)+(EPSU-EPSO*SIN(TEIN)**2d0)**0.5d0))**2d0

    SIV(I)=((EPSU*COS(TEIN)-EPSO**0.5d0*(EPSU-EPSO*SIN(TEIN)**2d0)**0.5d0)/&
        (EPSU*COS(TEIN)+EPSO**0.5d0*(EPSU-EPSO*SIN(TEIN)**2d0)**0.5d0))**2d0
END DO

DEALLOCATE(EPSI_LOCAL)

END SUBROUTINE FRESNELC
