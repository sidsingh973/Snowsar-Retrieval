! -------------------------------------------------------------------------
!
SUBROUTINE BORN(K,VFI,PCIMM,EPSI,EICE,GB6,GC6,GF6,GS6,NUM)
!
! -------------------------------------------------------------------------
!
!   CODE ORIGINALLY OBTAINED IN MATLAB FROM MATZLER, MODIFIED BY MIKE... SEE
!     VERSION HISTORY
!
!   CALCULATES THE SCATTERING COEFFICIENT USING BORN APPROXIMATION
!
!   [GB6,GC6,GF6,GS6] = BORNA(K,VFI,PCIMM,EPSI,EICE,EPSEFF,KP)
!       GB6: 6-FLUX BACK SCATTERING COEFFICIENT
!       GC6: 6-FLUX CROSS SCATTERING COEFFICIENT
!       GF6: 6-FLUX FORWARD SCATTERING COEFFICIENT
!       GS6: 6-FLUX SCATTERING COEFFICIENT
!       K:   WAVE NUMBER
!       VFI: VOLUME FRACTION OF ICE
!       PCIMM: CORRELATION LENGTH [mm]
!       EPSI: DIELECTRIC CONSTANT OF SNOW
!       EICE: DIELECTRIC CONSTANT OF SNOW
!       EPSEFF: EFFECTIVE PERMITTIVITY
!
!   VERSION HISTORY:
!      1.0     WI 27.05.98
!      2.0     WI 31.03.05.  TRANSLATED TO FORTRAN BY MIKE.  BORNA AND
!                             BORNSNK COMBINED INTO ONE.  CALL TO POLDER
!                             IS USED TO COMPUTE EFFECTIVE PERMITTIVITY
!      3.0    MD  1 APR 05 TRANSLATED TO FORTRAN FROM MATLAB
!      3.1    MD 22 DEC 11 Added Unity,NegUnity variables to prevent compiler warnings
!
!   USES:
!
!   COPYRIGHT (C) 1998 BY THE INSTITUTE OF APPLIED PHYSICS,
!   UNIVERSITY OF BERN, SWITZERLAND

IMPLICIT NONE

INTEGER, INTENT(IN) :: NUM
REAL(8), INTENT(IN) :: K,VFI(NUM),PCIMM(NUM),EPSI(NUM),EICE
REAL(8), INTENT(OUT) :: GB6(NUM),GC6(NUM),GF6(NUM),GS6(NUM)
REAL(8),DIMENSION(:),ALLOCATABLE :: PCI,A,A3,EA,EA3,K1,K3,KP,MUC,AA,XX,BB,BT,BF,&
BTOT, EPSEFF,NEGMUC
Real(8),Parameter,Dimension(1) :: Unity=1.,NegUnity=-1.
INTEGER STEPS,ROW,ARG_LENGTH(2),I



ALLOCATE( PCI(NUM),A(NUM),A3(NUM),EA(NUM),EA3(NUM),K1(NUM),K3(NUM),KP(NUM),&
MUC(NUM),AA(NUM),XX(NUM),BB(NUM),BT(NUM),BF(NUM),BTOT(NUM),&
EPSEFF(NUM),NEGMUC(NUM) )

! 0)  CONSTANTS AND CONVERSION
STEPS=11
PCI=PCIMM*0.001d0

! 1)  COMPUTE FIELD FACTOR AND DEPOLARIZATION RATIO, FROM BORNSNK
!     'A' AFTER NOTE 10, MATZLER 1997, COMMENT AND CALL FROM BORNSNK
CALL SNOWAO(VFI,A,NUM)

! 2)  COMPUTE EFFECTIVE PERMITTIVITY USING NEW FUNCTION POLDER.M

!CALL POLDER(VFI,A,EICE,EPSI,EPSEFF,NUM)  !Jinmei commented on Dec22,2015, because MEMLS3 code does not contain this
EPSEFF=EPSI

! 3)  COMPUTE KP, THE SQUARED RATIO BETWEEN INTERNAL / EXTERNAL FIELDS
!     THIS CODE ORIGINALLY IN BORNSNK, STARTING AT LINE 41
A3=1-2d0*A
EA=EPSEFF*(1-A)+A
EA3=EPSEFF*(1-A3)+A3
K1=(EA/(EA+A*(EICE-1)))**2d0
K3=(EA3/(EA3+A3*(EICE-1)))**2d0
KP=(2d0*K1+K3)/3d0

! 4)  COMPUTE SCATTERING COEFFICIENTS, FROM BORNA, STARTING AT LINE 34

!NOTE: THE NEXT LINE CONSISTENT WITH MATZLER AND WIESMANN 99 PAPER EQUATION (7).
!  BASED ON CORRESPONDENCE WITH MATZLER, EPSEFF=N^2

MUC=((EPSEFF-1)/EPSEFF)**0.5d0

AA=2d0*(PCI*K)**3d0*K*VFI*(1-VFI)*(EICE-1)**2d0*KP
XX=PCI*K*EPSI**0.5d0

! 5)  TRIPLE INTEGRATION

!  A) BACKWARD SCATTERING
ARG_LENGTH=(/1, NUM/)
NEGMUC=-1.0d0*MUC
!!!CALL INTEGRMUI(XX,MUC,1.,-1.,-1.*MUC,STEPS,NUM,BB,ARG_LENGTH)
!CALL INTEGRMUI(XX,MUC,1.,-1.,NEGMUC,STEPS,NUM,BB,ARG_LENGTH) !Orig: 22 Dec
CALL INTEGRMUI(XX,MUC,1.0d0,NegUnity,NEGMUC,STEPS,NUM,BB,ARG_LENGTH) !MDrev
!  B) TRANSVERSE SCATTERING
ARG_LENGTH=(/NUM, NUM/)
!CALL INTEGRMUI(XX,MUC,1.,-1.*MUC,MUC,STEPS,NUM,BT,ARG_LENGTH)
CALL INTEGRMUI(XX,MUC,1.0d0,NEGMUC,MUC,STEPS,NUM,BT,ARG_LENGTH)
!  C) FORWARD SCATTERING
ARG_LENGTH=(/NUM, 1/)
!CALL INTEGRMUI(XX,MUC,1.,MUC,1.,STEPS,NUM,BF,ARG_LENGTH) !Orig: 22 Dec 11
CALL INTEGRMUI(XX,MUC,1.,MUC,Unity,STEPS,NUM,BF,ARG_LENGTH)

! 6)  CALCULATION OF SCATTERING COEFFICIENTS

BTOT=BB+BT+BF
GB6=AA*BB
GC6=0.25d0*AA*BT
GF6=AA*BF
GS6=AA*BTOT

DEALLOCATE( PCI,A,A3,EA,EA3,K1,K3,KP,MUC,AA,XX,BB,BT,BF,BTOT, EPSEFF,NEGMUC)

END SUBROUTINE BORN
