! -------------------------------------------------------------------------
!
SUBROUTINE EPSIICE(TI,FREQ,EI_ICE,NUM)
!
! -------------------------------------------------------------------------
!
!   CODE ORIGINALLY OBTAINED IN MATLAB FROM MATZLER
!
!   CALCULATES THE DIELECTRIC PERMITTIVITY OF ICE
!   AFTER HUFFORD, MITZIMA AND MATZLER
!
!   EI_ICE = EPSICE(TI,FREQ)
!      EI_ICE:  DIELECTRIC PERMITTIVITY OF ICE (Imaginary part only)
!      TI:    TEMPERATURE [K]
!      FREQ:  FREQUENCY [GHZ]
!
!   VERSION HISTORY:
!      1.0    WI 15.7.95
!      2.0    MD 1 APR 05 TRANSLATED TO FORTRAN FROM MATLAB
!      3.0    JP REVISED ACCORDING TO MEMLS3&A
!
!   USES: NONE
!
!   COPYRIGHT (C) 1997 BY THE INSTITUTE OF APPLIED PHYSICS,
!   UNIVERSITY OF BERN, SWITZERLAND

IMPLICIT NONE

INTEGER, INTENT(IN) :: NUM
REAL(8), INTENT(IN) :: TI(NUM),FREQ
REAL(8), INTENT(OUT) ::  EI_ICE(NUM)
REAL(8) PP(NUM),B,B1,B2,DB(NUM),BETA(NUM),ALPHA(NUM)

B1=0.0207d0
B2=1.16d-11
B=335.0d0

DB=EXP(-9.963d0 + 0.0372d0*(TI-273d0))
BETA=((B1*EXP(B/TI))/(TI*(EXP(B/TI)-1)**2d0)) + B2*FREQ**2d0 +DB

PP=(300d0/TI)-1
ALPHA=(0.00504d0+0.0062d0*PP)*EXP(-22.1d0*PP)
EI_ICE=ALPHA/FREQ+BETA*FREQ

END SUBROUTINE EPSIICE
